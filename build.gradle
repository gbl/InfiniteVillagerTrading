plugins {
	id 'fabric-loom' version '0.12-+'
	id "com.modrinth.minotaur" version "2.+"
	id "com.matthewprenger.cursegradle" version "1.4.0"
}

repositories {
	maven {
		url = "https://maven.fabricmc.net"
	}
	maven {
		url = "https://minecraft.guntram.de/maven/"
	}
	maven {
		url = "https://maven.terraformersmc.com/releases/"
	}
	maven {
		url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
	}

}

sourceCompatibility = 17
targetCompatibility = 17

ext.Versions = new Properties()
Versions.load(file("Versionfiles/mcversion-22w45a.properties").newReader())

archivesBaseName = "infinitevillagertrading"
ext.projectVersion = "1.1"

version = "${Versions['minecraft_version']}-fabric${Versions['fabric_versiononly']}-${project.projectVersion}"

loom {
    mixin.defaultRefmapName = "InfiniteVillagerTrading-refmap.json";
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
    filesMatching("plugin.yml") {
        expand "version": project.version
    }
}

dependencies {
    minecraft  "com.mojang:minecraft:${Versions['minecraft_version']}"
    mappings   "net.fabricmc:yarn:${Versions['yarn_mappings']}:v2"
    modImplementation "net.fabricmc:fabric-loader:${Versions['loader_version']}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${Versions['fabric_version']}"
    modImplementation "com.terraformersmc:modmenu:${Versions['modmenu_version']}"

    implementation ("org.spigotmc:spigot-api:1.16.2-R0.1-SNAPSHOT") {
        exclude group: "com.google.guava", module: "guava"
        exclude group: "com.google.code.gson", module: "gson"
    }
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

jar {
	from "LICENSE"
}

import com.modrinth.minotaur.TaskModrinthUpload

modrinth {
    projectId = 'infinitevillagertrading'
    versionName = project.archivesBaseName
    uploadFile = remapJar
}

curseforge {
    apiKey = System.getenv("CURSEFORGE_TOKEN") ?: "0"
    project {
        id = '392254'
        releaseType = 'release'
        addGameVersion("${Versions['minecraft_version']}")
        addGameVersion("Java "+targetCompatibility)
        addGameVersion("Fabric")
        mainArtifact(remapJar)
    }
    options {
        forgeGradleIntegration = false
    }
}
